plugins {
    id "org.springframework.boot" version "2.6.4"
    id "java"
    id "idea"
    id "com.diffplug.spotless" version "6.3.0"
    id "com.github.ben-manes.versions" version "0.42.0"
    id 'io.spring.dependency-management' version "1.0.11.RELEASE"
}


repositories {
    mavenCentral()
}

ext.javaJwtVersion = "3.18.3"
ext.comonsIoVersion = "2.11.0"
ext.tikaVersion = "2.3.0"
ext.poiVersion = "5.2.1"
ext.sentryVersion = "6.0.0-alpha.2"
ext.springdocVersion = "1.6.6"
ext.hibernateTypesVersion = "2.14.0"
ext.p6spyVersion = "1.8.0"

dependencies {
    implementation "org.springframework.boot:spring-boot-starter-web"
    implementation "org.springframework.boot:spring-boot-starter-aop"
    implementation "org.springframework.boot:spring-boot-starter-security"
    implementation "org.springframework.boot:spring-boot-starter-actuator"
    implementation "org.springframework.boot:spring-boot-starter-data-jpa"
    implementation "org.springframework.boot:spring-boot-configuration-processor"
    implementation "org.apache.commons:commons-lang3"
    implementation "com.querydsl:querydsl-core"
    implementation "com.querydsl:querydsl-jpa"
    implementation "com.h2database:h2"
    implementation "com.github.gavlyukovskiy:p6spy-spring-boot-starter:$p6spyVersion"
    implementation "com.auth0:java-jwt:$javaJwtVersion"
    implementation "commons-io:commons-io:$comonsIoVersion"
    implementation "org.apache.tika:tika-core:$tikaVersion"
    implementation "org.apache.poi:poi:$poiVersion"
    implementation "org.apache.poi:poi-ooxml:$poiVersion"
    implementation "io.sentry:sentry-spring-boot-starter:$sentryVersion"
    implementation "io.sentry:sentry-logback:$sentryVersion"
    implementation "org.springdoc:springdoc-openapi-ui:$springdocVersion"
    implementation "org.springdoc:springdoc-openapi-webmvc-core:$springdocVersion"
    implementation "org.springdoc:springdoc-openapi-data-rest:$springdocVersion"
    implementation "com.vladmihalcea:hibernate-types-52:$hibernateTypesVersion"
    implementation "org.flywaydb:flyway-core"
    testAnnotationProcessor "org.projectlombok:lombok"
    annotationProcessor "org.projectlombok:lombok"
    annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"
    annotationProcessor "com.querydsl:querydsl-apt:${dependencyManagement.importedProperties['querydsl.version']}:jpa"
    annotationProcessor("jakarta.persistence:jakarta.persistence-api")
    annotationProcessor("jakarta.annotation:jakarta.annotation-api")
}

java.sourceCompatibility = JavaVersion.VERSION_17

test {
    exclude '**/*'
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

bootJar {
    archiveFileName = 'demo.jar'
}

spotless {
    java {
        removeUnusedImports()

        googleJavaFormat('1.13.0')
    }
}

task cleanGeneatedDir(type: Delete) {
    // 인텔리제이 annotation processor 가 생성한 Q클래스가 clean 태스크로 삭제되는 게 불편하다면 둘 중에 하나를 선택
    delete file('src/main/generated')
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}
